# Gramaticas


<INICIO> ::= <INSTRUCCIONES>

  

<INSTRUCCIONES> ::= <INSTRUCCIONES>  <INSTRUCCION>

| <INSTRUCCION>

  

<INSTRUCCION> ::= <IMPRESION>

| <DECLARACION>

| <ASIGNACION>

| <DECLARACION_VEC>

| <ASIGNACION_VEC>

| <DECLARACION_LIST>

| <SIF>

| <CFOR>

| <BREAKK>

| <CONTINUEE>

| <RETURNN>

| <SDO_WHILE>

| <SWHILE>

| <METODO>

| <STARTWITHH>

| <LLAMADA> FINCADENA

| <APPENDD>

| <REMOVEE>

| <LENGHTT>

| error FINCADENA

  

<IMPRESION> ::= IMPRIMIR PAR1 <EXPRESION> PAR2 FINCADENA

  

<DECLARACION> ::= <MUTABILIDAD> ID DOSPUNTOS <TIPOS> IGUAL <EXPRESION> FINCADENA

  

<DECLARACION_VEC> ::= <MUTABILIDAD> ID DOSPUNTOS <TIPOS> CORCHETE1 CORCHETE2 IGUAL CORCHETE1 <LISTA_VALORES> CORCHETE2 FINCADENA

  

<DECLARACION_LIST> ::= LIST MENOR <TIPOS> MAYOR ID IGUAL NEW LIST PAR1 PAR2 FINCADENA

  

<LISTA_VALORES> ::= <LISTA_VALORES> COMA <EXPRESION>

| <EXPRESION>

  

<MUTABILIDAD> ::= CONST

| VAR

  

<ASIGNACION> ::= ID IGUAL <EXPRESION> FINCADENA

  

<ASIGNACION_VEC> ::= ID CORCHETE1 <EXPRESION> CORCHETE2 IGUAL <EXPRESION> FINCADENA

  

<METODO> ::= <TIPOS> ID PAR1 <PARAMS> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

| <TIPOS> ID PAR1 PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

  

<PARAMS> ::= <PARAMS> COMA <TIPOS> ID

| <TIPOS> ID

  

<STARTWITHH> ::= STARTWITH ID PAR1 <PARAMSCALL> PAR2 FINCADENA

| STARTWITH ID PAR1 PAR2 FINCADENA

  

<LLAMADA> ::= ID PAR1 <PARAMSCALL> PAR2

| ID PAR1 PAR2

  

<PARAMSCALL> ::= <PARAMSCALL> COMA <EXPRESION>

| <EXPRESION>

  

<SIF> ::= IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

  

<CFOR> ::= FOR PAR1 <ASIGNACION>  <EXPRESION> FINCADENA <ACTUALIZA_FOR> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

  

<ACTUALIZA_FOR> ::= ID IGUAL <EXPRESION>

  

<BREAKK> ::= BREAK FINCADENA

  

<CONTINUEE> ::= CONTINUE FINCADENA

  

<RETURNN> ::= RETURN <EXPRESION> FINCADENA

| RETURN FINCADENA

  

<SWHILE> ::= WHILE PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

  

<SDO_WHILE> ::= DO LLAVE1 <INSTRUCCIONES> LLAVE2 WHILE PAR1 <EXPRESION> PAR2 FINCADENA

  

<APPENDD> ::= ID PUNTO APPEND PAR1 <EXPRESION> PAR2 FINCADENA

  

<REMOVEE> ::= ID PUNTO REMOVE PAR1 <EXPRESION> PAR2 FINCADENA

  

<LENGHTT> ::= LENGHT PAR1 <EXPRESION> PAR2

  

<TIPOS> ::= INT

| DOUBLE

| STRING

| CHAR

| BOOL

| VOID

  

<EXPRESION> ::= MENOS <EXPRESION> %prec UMENOS

| <EXPRESION> POTENCIA <EXPRESION>

| <EXPRESION> DIVISION <EXPRESION>

| <EXPRESION> MULTIPLICACION <EXPRESION>

| <EXPRESION> MODULO <EXPRESION>

| <EXPRESION> MAS <EXPRESION>

| <EXPRESION> MENOS <EXPRESION>

| <EXPRESION> IGUAL IGUAL <EXPRESION>

| <EXPRESION> DIFERENTE <EXPRESION>

| <EXPRESION> MENORIGUAL <EXPRESION>

| <EXPRESION> MAYORIGUAL <EXPRESION>

| <EXPRESION> MENOR <EXPRESION>

| <EXPRESION> MAYOR <EXPRESION>

| NOT <EXPRESION> %prec NOT

| <EXPRESION> XOR <EXPRESION> %prec XOR

| <EXPRESION> AND <EXPRESION> %prec AND

| <EXPRESION> OR <EXPRESION> %prec OR

| <LLAMADA>

| <LENGHTT>

| ENTERO

| DECIMAL

| CADENA

| CARACTER

| TRUE

| FALSE

| ID

| ID CORCHETE1 <EXPRESION> CORCHETE2

| PAR1 <EXPRESION> PAR2
;